apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: "de.mannodermaus.android-junit5"

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.example.rries.sampleapp"
        minSdkVersion 26
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner getInstrumentation()
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.md'
        exclude 'META-INF/LICENSE-notice.md'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:design:28.0.0'

    // Room components
    implementation "android.arch.persistence.room:runtime:$roomVersion"
    kapt "android.arch.persistence.room:compiler:$roomVersion"
    androidTestImplementation "android.arch.persistence.room:testing:$roomVersion"

    // Lifecycle components
    implementation "android.arch.lifecycle:extensions:$archLifecycleVersion"
    kapt "android.arch.lifecycle:compiler:$archLifecycleVersion"

    // Coroutines
    api "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines"

    //Dagger
    implementation "com.google.dagger:dagger:$dagger"
    kapt "com.google.dagger:dagger-compiler:$dagger"

    // (Required) Writing and executing Unit Tests on the JUnit Platform
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'

    // (Optional) If you need "Parameterized Tests"
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.3.1'

    // (Optional) If you also have JUnit 4-based tests
    testImplementation 'junit:junit:4.12'
    testRuntimeOnly 'org.junit.vintage:junit-vintage-engine:5.3.1'

    // (Required) Writing tests for JUnit Jupiter
    androidTestImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'

    // (Required) The instrumentation test companion library
    androidTestImplementation 'de.mannodermaus.junit5:android-instrumentation-test:0.2.2'

    // (Required) Runtime dependencies to orchestrate the execution on-device
    androidTestRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.3.1"
    androidTestRuntimeOnly "org.junit.platform:junit-platform-runner:1.3.1"
    androidTestRuntimeOnly "de.mannodermaus.junit5:android-instrumentation-test-runner:0.2.2"

    //Cucumber
    androidTestImplementation 'io.cucumber:cucumber-android:4.2.0'
    androidTestImplementation 'io.cucumber:cucumber-picocontainer:4.2.0'

    //Espresso
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-intents:3.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

// Run ./gradlew connectedCheck -Pcucumber to run Cucumber Tests
def getInstrumentation() {
    project.hasProperty('cucumber') ?
            'com.example.rries.sampleapp.cucumbertest.CucumberTestRunner' :
            'android.support.test.runner.AndroidJUnitRunner'
}